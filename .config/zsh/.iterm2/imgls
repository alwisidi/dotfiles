#!/usr/bin/perl
use v5.14;
use strict;
use utf8;
use warnings;
use File::stat;
use IO::Select;
use IPC::Open3;
use File::Spec;
use MIME::Base64;
use File::Basename;
use Symbol 'gensym';
use Encode qw(decode);
use List::Util qw(max);
use POSIX qw(strftime floor modf);
use Getopt::Long qw(:config no_permute pass_through require_order);
STDERR->autoflush(1);
STDOUT->autoflush(1);
binmode STDOUT, ':encoding(UTF-8)';
binmode STDERR, ':encoding(UTF-8)';
my $prog = basename $0;
sub usage {
    my $leader = "usage: $prog";
    say STDERR
        "usage: $prog",
        " [--width #] [--height #] [--[no]preserve_ratio] [--[no]dimensions]",
        "\n",
        ' ' x length($leader),
        " [--[no]unknown] [ls options] [file ...]";
    exit shift;
}
my $def_image_width     = 3;
my $def_image_height    = 1;
my $def_preserve_ratio  = 'true';
my %imgparams = (
    inline              => 1,
    height              => $def_image_height,
    width               => $def_image_width,
    preserveAspectRatio => $def_preserve_ratio,
);
my %failed_types;
my %stat_cache;
my $curtime = time();
my $sixmonths = (365 / 2) * 86400;
my %opts = (
    height      => \$imgparams{'height'},
    width       => \$imgparams{'width'},
);
get_options(\%opts);
my $dims_methods = init_dims_methods();
my $dims_method  = find_dims_methods($dims_methods);
my ($one_pixel_black, $one_pixel_black_len) = get_black_pixel_image();
my $do_newline;
my $dot_filter = $opts{'A'} ? qr/^\.{1,2}$/ : qr/^\./;
$dot_filter    = undef          if $opts{'a'};
my $do_header = @ARGV > 1 ? 1 : 0;
if (@ARGV <= 1) {
    push @ARGV, '.'     if @ARGV == 0;
}
my (@files, @dirs);
for (@ARGV) {
    if (! -e _lstat($_)) {
        say STDERR "$prog: $_: No such file or directory";
    }
    elsif (-f _lstat($_)) {
        push @files, $_;
    }
    else {
        push @dirs, $_;
    }
}
@files = ls_sort(@files);
@dirs  = ls_sort(@dirs);
if ($opts{'d'}) {
    push @files, @dirs;
    @dirs = ();
}
do_ls(undef, @files)    if @files;
while (@dirs) {
    my $path = shift @dirs;
    if (! -e $path) {
        say STDERR "$prog: $path: No such file or directory";
        next;
    }
    my (@f, @d);
    get_dir_content($path, $dot_filter, \@f, \@d) or
        next;
    do_ls($path, @f, @d);
    if ($opts{'R'}) {
        push @dirs, grep { ! /\.\.?$/ } @d;
    }
    $do_newline++;
}
sub write_image {
    my ($file, $size) = @_;
    my $encoded;
    $imgparams{'name'} = encode_base64($file, '');
    $imgparams{'size'} = $size;
    if (ref $file eq '')  {
        my $bytes = get_image_bytes($file);
        $encoded = encode_base64($bytes)                if defined $bytes;
    }
    if (! $encoded or ref $file eq 'SCALAR') {
        $encoded = $one_pixel_black;
        $imgparams{'name'} = encode_base64('one_pixel_black', '');
        $imgparams{'size'} = $one_pixel_black_len;
    }
    printf "%s%s%s;File=%s:%s%s",
            "\033", "]", "1337",
            join(';', map { $_ . '=' . $imgparams{$_} } keys %imgparams),
            $encoded,
            "\007";
}
sub get_options {
    local $SIG{__WARN__} = sub { say "$prog: ", $_[0]; usage(1) };
    Getopt::Long::Configure(qw/no_ignore_case bundling no_passthrough/);
    my $result = GetOptions(\%opts,
        'dimensions!'           => sub { $opts{'unknown'}++; $opts{$_[0]} = $_[1] },
        'height=s',
        'method=s',
        "preserve_ratio!"       => sub { $imgparams{'preserveAspectRatio'} = $_[1] ? 'true' : 'false' },
        'unknown!'              => sub { $opts{'dimensions'}++; $opts{$_[0]} = $_[1] },
        'width=s',
        'D=s',
        't'                     => sub { delete $opts{'S'}; $opts{'t'}++ },
        'S'                     => sub { delete $opts{'t'}; $opts{'S'}++ },
        qw/ A F R T a d h i k l n o p r s u y /, 'c|U',
    );
    $opts{'d'} and delete $opts{'R'};
    $opts{'D'} and delete $opts{'T'};
    $opts{'n'} and $opts{'l'}++;
    $opts{'o'} and $opts{'l'}++;
    $opts{'s'} and $opts{'show_blocks'}++;
}
sub get_dir_content {
    my ($path, $filter, $filesref, $dirsref) = @_;
    my $dh;
    unless (opendir($dh, $path)) {
        say STDERR "Unable to open directory $path: $!";
        return undef;
    }
    while (readdir($dh)) {
        next if defined $filter and $_ =~ /$filter/;
        my $p = "$path/$_";
        if (-d _lstat($p)) {
            push @$dirsref, $p;
        }
        else {
            push @$filesref, $p;
        }
    }
    closedir $dh;
    return 1;
}
sub do_ls {
    my $path = shift;
    my $blocks_total = 0;
    my (@hfiles, %widths, $st);
    for my $file (ls_sort(@_)) {
        #say "FILE: $file";
        my %h;
        $h{'file'}      = $file;
        $h{'filename'}  = defined $path ? (split /\//, $file)[-1] : $file;
        $h{'st'}        = $st = _lstat($file);
        $h{'ino'}       = $st->ino                      if $opts{'i'};
        $h{'bytes'}     = $st->size;
        $h{'bytesh'}    = format_human($h{'bytes'})     if $opts{'h'};
        $h{'dims'}      = get_dimensions($file)         if $opts{'dimensions'} and -f $st && -r $st && $h{'bytes'};
        $h{'nlink'}     = $st->nlink                    if $opts{'s'} or $opts{'l'};
        if ($opts{'show_blocks'} or $opts{'l'}) {
            $h{'blocks'} = $st->blocks;
            if ( ! -d $st and ($opts{'a'} or $h{'filename'} !~ /^\.[^.]+/)) {
                $blocks_total += $st->blocks;
            }
        }
        if ($opts{'l'}) {
            $h{'lsmodes'} = Stat::lsMode::format_mode($st->mode);
            $h{'owner'} = ($opts{'n'} ? $st->uid : getpwuid $st->uid) // $st->uid;
            $h{'group'} = ($opts{'n'} ? $st->gid : getgrgid $st->gid) // $st->gid       if not $opts{'o'};
            $h{'time'}  = format_time($opts{'c'} ? $st->ctime : $st->mtime);
        }
        push @hfiles, \%h;
        $widths{'dim_w'}  = max(defined $h{'dims'} ? length($h{'dims'}{'width'})  : 0, $widths{'dim_w'} // 0);
        $widths{'dim_h'}  = max(defined $h{'dims'} ? length($h{'dims'}{'height'}) : 0, $widths{'dim_h'} // 0);
        for my $key (qw/blocks ino bytes bytesh owner group nlink/) {
            $widths{$key} = max(length($h{$key}), $widths{$key} // 0)   if exists $h{$key};
        }
    }
    print "\n"                                          if $path and $do_newline;
    print "$path:\n"                                    if $path and $do_header++;
    say "total ", $blocks_total / ($opts{'k'} ? 2 : 1)   if $path and ! $opts{'d'} and ($opts{'show_blocks'} or $opts{'l'});
    for my $h (@hfiles) {
        if (! -f $h->{'st'} or ! $h->{'bytes'} or ($opts{'dimensions'} and ! $h->{'dims'} and ! $opts{'unknown'})) {
            write_image \$one_pixel_black, $one_pixel_black_len;
        }
        else {
            write_image $h->{'file'}, $h->{'bytes'};
        }
        if ($opts{'dimensions'}) {
            if ($widths{'dim_w'} or $widths{'dim_h'}) {
                my $min_w = $widths{'dim_w'} // 1;
                my $min_h = $widths{'dim_h'} // 1;
                if ($h->{'dims'}{'width'} or $h->{'dims'}{'height'}) {
                    printf " [%*d x %*d] ", $min_w, $h->{'dims'}{'width'} // 0, $min_h, $h->{'dims'}{'height'} // 0;
                }
                else {
                    printf " %*s   %*s   ", $min_w, ' ', $min_h, ' ';
                }
            }
        }
        printf " %*d",  $widths{'ino'},    $h->{'ino'}                  if $opts{'i'};
        printf " %*d",  $widths{'blocks'}, $h->{'blocks'}               if $opts{'s'};
        printf " %s",   $h->{'lsmodes'}                                 if exists $h->{'lsmodes'};
        printf " %*s",  $widths{'nlink'},  $h->{'nlink'}                if exists $h->{'nlink'};
        printf " %*s",  $widths{'owner'},  $h->{'owner'}                if exists $h->{'owner'};
        printf "  %*s", $widths{'group'},  $h->{'group'}                if exists $h->{'group'};
        if ($opts{'l'}) {
            printf "  %*d", $widths{'bytes'},  $h->{'bytes'}            if ! $opts{'h'};
            printf "  %4s", $h->{'bytesh'}                              if $opts{'h'};
        }
        printf " %s",   $h->{'time'}                                    if exists $h->{'time'};
        print  " ",     Encode::decode('UTF-8', defined $path ? (split /\//, $h->{'file'})[-1] : $h->{'file'});
        printf "%s",    get_F_type($h->{'st'})                          if $opts{'F'} or $opts{'p'};
        print "\n";
    }
}
sub get_dimensions {
    my $file = shift;
    my ($ret, $ext);
    $file =~ /\.([^.]+)$/ and $ext = $1;
    if ($dims_method and (!$ext or ($ext and ! exists $failed_types{$ext}))) {
        if (ref $dims_method->{'prog'} eq 'CODE') {
            $ret = $dims_method->{'format'}->($file);
        }
        else {
            my ($stdout, $stderr, $exit) = runcmd($dims_method->{'prog'}, @{$dims_method->{'args'}}, $file);
            if ($stdout) {
                $ret = $dims_method->{'format'}->($stdout);
            }
        }
    }
    $failed_types{$ext}++       if ! $ret and $ext;
    return $ret;
}
sub runcmd {
    my $prog = shift;
    my $pid = open3(my $in, my $out, my $err = gensym, $prog, @_);
    my ($out_buf, $err_buf) = ('', '');
    my $select = new IO::Select;
    $select->add($out, $err);
    while (my @ready = $select->can_read(5)) {
        foreach my $fh (@ready) {
            my $data;
            my $bytes = sysread($fh, $data, 1024);
            if (! defined( $bytes) && ! $!{ECONNRESET}) {
                die "error running cmd: $prog: $!";
            }
            elsif (! defined $bytes or $bytes == 0) {
                $select->remove($fh);
                next;
            }
            else {
                if    ($fh == $out) { $out_buf .= $data; }
                elsif ($fh == $err) { $err_buf .= $data; }
                else {
                    die 'unexpected filehandle in runcmd';
                }
            }
        }
    }
    waitpid($pid, 0);
    return ($out_buf, $err_buf, $? >> 8);
}
sub init_dims_methods {
    return [
        {
            prog        => 'sips',
            args        => [ '-g', 'pixelWidth', '-g', 'pixelHeight' ],
            format      => sub {
                my $out = shift;
                return ($out =~ /pixelWidth: (\d+)\s+pixelHeight: (\d+)/s) ? { width => $1, height => $2 } : undef;
            }
        },
        {
            prog        => 'mdls',
            args        => [ '-name', 'kMDItemPixelWidth', '-name', 'kMDItemPixelHeight' ],
            format      => sub {
                my $out = shift;
                my %dim;
                for my $d (qw /Width Height/) {
                    $dim{$d} = $1               if $out =~ /kMDItemPixel$d\s*=\s*(\d+)$/m;
                }
                return ($dim{'Width'} and $dim{'Height'}) ? { width => $dim{'Width'}, height => $dim{'Height'} } : undef;
            }
        },
        {
            prog        => 'php',
            args        => [ '-r', q/$a = getimagesize("$argv[1]"); if ($a==FALSE) exit(1); else { echo $a[0] . "x" .$a[1]; exit(0); }/ ],
            format      => sub {
                my $out = shift;
                return undef unless $out;
                my @d = split /x/, $out;
                return { width => $d[0], height => $d[1] };
            }
        },
        {
            prog        => 'exiftool',
            args        => [ '-s', '-ImageSize' ],
            format      => sub {
                my $out = shift;
                return ($out =~ /ImageSize\s+:\s+(\d+)x(\d+)/) ? { width => $1, height => $2 } : undef;
            }
        },
        {
            prog        => \&have_Image_Size,
            format      => \&call_Image_Size,
            name        => 'Image::Size',
        }
    ]
};
sub find_dims_methods {
    my $methods = shift;
    if ($opts{'method'}) {
        @$methods = grep {
            (exists $_->{'name'} and (lc($_->{'name'}) eq lc($opts{'method'}))) or
            ($_->{'prog'} eq $opts{'method'}) } @$methods;
    }
    for (@$methods) {
        if (ref $_->{'prog'} eq 'CODE' and $_->{'prog'}->()) {
            return $_;
        }
        elsif (my $choice = which($_->{'prog'})) {
            $_->{'prog'} = $choice;
            return $_;
        }
    }
    say STDERR "$prog: no methods found to obtain image dimensions. Tried: ",
        map { "\n   " . (exists $_->{'name'} ? $_->{'name'} : $_->{'prog'}) } @$methods;
    exit 1;
}
sub have_Image_Size {
    eval "require Image::Size";
    return Image::Size->can('imgsize');
}
sub call_Image_Size {
    my $file = shift;
    my ($w, $h, $type) = Image::Size::imgsize($file);
    if (defined $w and defined $h) {
        if ($type eq 'BMP') {
            $w = (2**32) - $w   if $w > 2**31;
            $h = (2**32) - $h   if $h > 2**31;
        }
        return { width => $w, height => $h };
    }
    return undef;
}
sub get_image_bytes {
    my $file = shift;
    $/ = undef;
    open (my $fh, "<", $file)
        or return undef;
    my $filebytes = <$fh>;
    chomp $filebytes;
    close $fh;
    return $filebytes;
}
sub ls_sort {
    return if ! (@_ or scalar @_ > 1);
    if ($opts{'t'}) {
        @_ = ($opts{'y'} or $ENV{'LS_SAMESORT'}) ?
            sort { _lstat($b)->mtime <=> _lstat($a)->mtime || $b cmp $a } @_ :
            sort { _lstat($b)->mtime <=> _lstat($a)->mtime || $a cmp $b } @_;
    }
    elsif ($opts{'c'}) {
        @_ = sort { $a cmp $b } @_;
    }
    elsif ($opts{'u'}) {
        @_ = sort { _lstat($a)->atime <=> _lstat($b)->atime } @_;
    }
    elsif ($opts{'S'}) {
        @_ = sort { _lstat($b)->size <=> _lstat($a)->size || $a cmp $b } @_;
    }
    else {
        @_ = sort @_;
    }
    return $opts{'r'} ? reverse @_ : @_;
}
sub get_F_type {
    my $st = shift;
    return '/'  if -d $st and ($opts{'p'} or $opts{'F'});
    return ''   unless $opts{'F'};
    return '@'  if -l $st;
    return '|'  if -p $st;
    return '='  if -S $st;
    return '*'  if -x $st;
    return '';
}
sub format_time {
    my $time = shift;
    my $fmt;
    if ($opts{'D'}) {
        $fmt = $opts{'D'};
    }
    elsif ($opts{'T'}) {
        $fmt = '%b %e %T %Y';
    }
    else {
        if ($time + $sixmonths > $curtime and $time < $curtime + $sixmonths) {
            $fmt = '%b %e %R';
        }
        else {
            $fmt = '%b %e  %Y';
        }
    }
    return strftime $fmt, localtime($time);
}
sub format_human {
    my $bytes = shift;
    my @units = ('B', 'K', 'M', 'G', 'T', 'P');
    my $scale = floor((length($bytes) - 1) / 3);
    my $float = $bytes / (1024 ** $scale);
    my ($frac, $int) = modf($float);
    if (length($bytes) < 3 or length($int) >= 2) {
        sprintf "%d%s", $frac <.5 ? $float : $float + 1, $units[$scale];
    }
    else {
        sprintf "%.1f%s", $float, $units[$scale];
    }
}
sub _lstat {
    my $file = shift;
    return $stat_cache{$file}   if exists $stat_cache{$file};
    if (my $s = lstat($file)) {
        return $stat_cache{$file} = $s;
    }
    return $file;
}
sub which {
  my ($exec) = @_;
    $exec or
        return undef;
    if ($exec =~ m#/# && -f $exec && -x _) {
        return $exec
    }
    foreach my $file ( map { File::Spec->catfile($_, $exec) } File::Spec->path) {
        -d $file and
            next;
        -x _ and
            return $file;
    }
    return undef;
}
sub get_black_pixel_image {
    my $one_pixel_black = 'iVBORw0KGgoAAAANSUhEUgAAAAEAAAABCAYAAAAfFcSJAAAADUlEQVQIW2NgYGD4DwABBAEAwS2OUAAAAABJRU5ErkJggg==';
    return ($one_pixel_black, length $one_pixel_black);
}
package Stat::lsMode;
sub format_mode {
    my $mode = shift;
    return undef        unless defined $mode;
    my @permchars  = qw(--- --x -w- -wx r-- r-x rw- rwx);
    my @ftypechars = qw(. p c ? d ? b ? - ? l ? s ? ? ?);
    $ftypechars[0] = '';
    my $setids     = ($mode & 07000) >> 9;
    my @permstrs   = @permchars[($mode & 0700) >> 6, ($mode & 0070) >> 3, $mode & 0007];
    my $ftype      = $ftypechars[($mode & 0170000) >> 12];
    if ($setids) {
        if ($setids & 01) {
            $permstrs[2] =~ s/([-x])$/$1 eq 'x' ? 't' : 'T'/e;
        }
        if ($setids & 04) {
            $permstrs[0] =~ s/([-x])$/$1 eq 'x' ? 's' : 'S'/e;
        }
        if ($setids & 02) {
            $permstrs[1] =~ s/([-x])$/$1 eq 'x' ? 's' : 'S'/e;
        }
    }
    join '', $ftype, @permstrs;
}
